---
layout: post
title: Neo4j Guide
tags: [Neo4j, Database, Graph Data]
color: rgb(224, 1, 152)
feature-img: "assets/img/post-cover/12-cover.png"
thumbnail: "assets/img/post-cover/12-cover.png"
authors: [QubitPi]
excerpt_separator: <!--more-->
---

Neo4j is a graph databases specialized for Data Science field.

<!--more-->

* TOC
{:toc}

## Books

* [Graph Algorithms: Examples in Spark and Neo4j]({{ "/assets/pdf/graph-algorithms.pdf" | relative_url}})
* [Intro Graph Data Science]({{ "/assets/pdf/graph-data-science-for-dummies.pdf" | relative_url}})
* [Graph Databases]({{ "/assets/pdf/graph-databases.pdf" | relative_url}})

## Concepts

In Neo4j, "label" is the type of vertex or edge. For example, a person vertex has a label of "person".

## Neo4j Query Language - Cypher

### Casing

Use camel case, starting with a lower-case character, for:

* functions
* properties
* variables
* parameters

### Desktop

#### Create New Project

Next, you will create a new project in Neo4j Desktop. You can only have a single DBMS running, so you must first stop
the DBMS that is started by clicking the `Stop` button:

![Error loading generic-stop-movie-database.png!]({{ "/assets/img/generic-stop-movie-database.png" | relative_url}})

You should now see that there is no active DBMS:

![Error loading generic-movie-database-stopped.png!]({{ "/assets/img/generic-movie-database-stopped.png" | relative_url}})

Add a new project by clicking `New` at the top of the sidebar:

![Error loading generic-create-project.png!]({{ "/assets/img/generic-create-project.png" | relative_url}})

This creates a project named `Project`:

![Error loading generic-project-created.png!]({{ "/assets/img/generic-project-created.png" | relative_url}})

You want your projects to be named something that describes the goal of the project. You can change the name of a
project by hovering over the area to the right of the project name and selecting the `Edit` button:

![Error loading generic-name-project.png!]({{ "/assets/img/generic-name-project.png" | relative_url}})

Then you type a name for the project and hit the "Check" button to save the name.

![Error loading generic-change-project-name.png!]({{ "/assets/img/generic-change-project-name.png" | relative_url}})

Now we see the project with the changed name:

![Error loading generic-project-named.png!]({{ "/assets/img/generic-project-named.png" | relative_url}})

To remove the project, hover over the Project row in the projects menu and click the trash bin icon that appears on the
right side

#### Creating a DBMS

A DBMS is a Neo4j Server instance that contains the `system` and `neo4j` databases, but can contain more databases
(multi-db feature of Neo4j 4.x Enterprise Edition)

In a Neo4j Desktop project, you can create one or more DBMSs. Next, you will create a local DBMS in a project and start
it.

In the project where you want to add the DBMS, click the `Add` button, and then select `Local DBMS`:

![Error loading generic-create-local-dbms.png!]({{ "/assets/img/generic-create-local-dbms.png" | relative_url}})

This opens a dialog where you will specify the details of the DBMS. You can use the default name for the DBMS, which is
Graph DBMS, but you should name it something that helps to identify the use case for the DBMS. Here we specify "MyDBMS"
as the name:

![Error loading generic-name-dbms.png!]({{ "/assets/img/generic-name-dbms.png" | relative_url}})

You _must_ also specify a password for the DBMS so enter a password that you will remember:

![Error loading generic-password-dbms.png!]({{ "/assets/img/generic-password-dbms.png" | relative_url}})

Neo4j Desktop will create a DBMS with the default version for Neo4j Desktop. However, you can select a different
version. Keep in mind that if there is a down arrow shown next to the version, this means that Neo4j Desktop will need
to download resources for that particular version of the DBMS. To do this you need to be connected to the Internet:

![Error loading generic-version-dbms.png!]({{ "/assets/img/generic-version-dbms.png" | relative_url}})

After you have specifed the details for the DBMS you want to create, you click the `Create` button:

![Error loading generic-create-dbms.png!]({{ "/assets/img/generic-create-dbms.png" | relative_url}})

And here is what you should see after the DBMS is successfully created:

![Error loading generic-dbms-created.png!]({{ "/assets/img/generic-dbms-created.png" | relative_url}})

Recall that you cannot have more than one DBMS started. Provided you have no other DBMSs started in Neo4j Desktop, you
can start your newly-created DBMS by hovering to the right of the DBMS name and clicking the `Start` button:

![Error loading generic-start-dbms.png!]({{ "/assets/img/generic-start-dbms.png" | relative_url}})

The DBMS will take some moments to start. After it is started, you should see something like this:

![Error loading generic-dbms-started.png!]({{ "/assets/img/generic-dbms-started.png" | relative_url}})

After the DBMS is started, you can access it with clients running on your system such as Neo4j Browser, Neo4j Bloom,
etc. In Neo4j Desktop, the DBMS is an Enterprise Server, but it can only be accessed locally.

You have now created your own DBMS!

### Creating Data

```
CREATE (:Movie { title:"The Matrix",released:1997 })
```

If case we also want to return the created data we can add a RETURN clause, which refers to the variable weâ€™ve assigned to our pattern elements.

```
CREATE (p:Person { name:"Keanu Reeves", born:1964 })
RETURN p
```

```
CREATE (a:Person { name:"Tom Hanks",
  born:1956 })-[r:ACTED_IN { roles: ["Forrest"]}]->(m:Movie { title:"Forrest Gump",released:1994 })
CREATE (d:Person { name:"Robert Zemeckis", born:1951 })-[:DIRECTED]->(m)
RETURN a,d,r,m
```

This is the part of the graph we just updated:

![Error loading neo4j-example3.png!]({{ "/assets/img/neo4j-example3.png" | relative_url}})

In most cases, we want to connect new data to existing structures. This requires that we know how to find existing
patterns in our graph data, which we will look at next.

### Matching Patterns

To find the data weâ€™ve created so far, we can start looking for all nodes labeled with the `Movie` label.

```
MATCH (movie:Movie)
RETURN movie
```

Here's the result:

![Error loading neo4j-match-eg.png!]({{ "/assets/img/neo4j-match-eg.png" | relative_url}})

We can also look for a specific person, like _Keanu Reeves_.

```
MATCH (person:Person { name:"Keanu Reeves" })
RETURN person
```

This query returns the matching node:

![Error loading neo4j-match-single-person-eg.png!]({{ "/assets/img/neo4j-match-single-person-eg.png" | relative_url}})

Note that not all properties of a node are required. In most cases you have key-properties like SSN, emails, geolocation
or age to look for.

We can also find more interesting connections, like for instance the movies titles that _Tom Hanks_ acted in and the
roles he played.

```
MATCH (person:Person { name:"Tom Hanks" })-[role:ACTED_IN]->(movie:Movie)
RETURN movie.title, role.roles
```

![Error loading neo4j-connection-example.png!]({{ "/assets/img/neo4j-connection-example.png" | relative_url}})

### Attaching Structures (Adding Nodes/Edges to Graph)

To extend the graph with new information, we
 
1. match the existing graph component, then
2. attach new nodes to them via relationships.

Let's, for example, add _Cloud Atlas_ as a new movie for _Tom Hanks_:

```
MATCH (tomHanks:Person { name: "Tom Hanks" })
CREATE (cloudAtlas:Movie { title: "Cloud Atlas", released: 2012 })
CREATE (tomHanks)-[role:ACTED_IN { roles: ['Zachry']}]->(cloudAtlas)
RETURN tomHanks, role, cloudAtlas
```

Here's what the structure looks like in the database:

![Error loading neo4j-expand-graph-eg.png!]({{ "/assets/img/neo4j-expand-graph-eg.png" | relative_url}})

> âš ï¸ A tricky aspect of `MATCH` with `CREATE` is that we get one row per matched pattern. This causes subsequent
> `CREATE` statements to be executed once for each row. In many cases this is what you want. Otherwise, please move the
> `CREATE` statement before the `MATCH`, or using `MERGE`, which we discuss next.

### Merge Operation

Whenever we are not sure if certain information already exists in the graph, we want to be able to repeat (idempotent)
the update operation. In Cypher `MERGE` has this function. It acts like a combination of `MATCH` or `CREATE`, but also
checks for the existence of data first before creating it. With `MERGE` you define a pattern to be found or created.
Usually, as with `MATCH` you only want to include the key property to look for in your core pattern. `MERGE` allows you
to provide additional properties you want to set `ON CREATE`.

```
MERGE (movie:Movie { title: "Cloud Atlas" })
ON CREATE SET movie.released = 2012
RETURN movie
```

![Error loading neo4j-merge-example.png!]({{ "/assets/img/neo4j-merge-example.png" | relative_url}})

If we don't know if our graph already contained _Cloud Atlas_ we could merge it in again.

> âš ï¸ `MERGE` has to check whether or not a match exists first. This can be very costly on large graphs, because it scans
> a large set of labeled nodes. You can alleviate some of that with indexes. But it's still not for free, so always
> consider `CREATE` before going to `MERGE`

`MERGE` can also assert that a relationship is only created once. For that to work you have to pass in both nodes from a
previous pattern match. For example:

```
MATCH (cloudAtlas:Movie { title:"Cloud Atlas" })
MATCH (tomHanks:Person { name:"Tom Hanks" })
MERGE (tomHanks)-[role:ACTED_IN]->(cloudAtlas)
ON CREATE SET role.roles =['Zachry']
RETURN tomHanks, role, cloudAtlas
```

![Error loading neo4j-merge-edge-eg.png!]({{ "/assets/img/neo4j-merge-edge-eg.png" | relative_url}})

In case case of bi-direction relationship, you can leave off the arrowhead. `MERGE` will then check for the relationship
in both direction, and create a new directed relationship if no matching relationship was found.

If you choose to pass in only one node from a preceding clause, `MERGE` offers an interesting functionality. It will
then only match within the direct neighborhood of the provided node for the given pattern, and, if not found create it.
For instance:

```
CREATE (y:Year { year:2014 })
MERGE (y)<-[:IN_YEAR]-(m10:Month { month:10 })
MERGE (y)<-[:IN_YEAR]-(m11:Month { month:11 })
RETURN y, m10, m11
```

![Error loading neo4j-merge-auto-eg.png!]({{ "/assets/img/neo4j-merge-auto-eg.png" | relative_url}})

Here there is no global search for the two "Month" nodes; they are only searched for in the context of the "2014 Year"
node.

### Filtering

So far we have matched patterns in the graph and always returned all results we found. Now we will look into options for
filtering the results and only return the subset of data that we are interested in. Those filter conditions are
expressed using the `WHERE` clause. This clause allows to use any number of boolean expressions, predicates, combined
with `AND`, `OR`, `XOR` and `NOT`. The simplest predicates are comparisons; especially equality.


```
MATCH (movie:Movie)
WHERE movie.title = "The Matrix"
RETURN movie
```

> âš ï¸ The query above is equivalent to this query which includes the condition in the pattern matching:
> 
> ```
> MATCH (movie:Movie { title: "The Matrix" })
> RETURN movie
> ```

The `WHER`E clause in the following example includes a regular expression match, a greater-than comparison, and a test
to see if a value exists in a list:

```
MATCH (person:Person)-[role:ACTED_IN]->(movie:Movie)
WHERE person.name =~ "K.+" OR movie.released > 2000 OR "Neo" IN role.roles
RETURN person, role, movie
```

Patterns can be used as predicates. Where `MATCH` expands the number and shape of patterns matched, a pattern predicate
restricts the current result set. It only allows the paths which satisfy the specified pattern:

```
MATCH (person:Person)-[:ACTED_IN]->(movie)
WHERE NOT (person)-[:DIRECTED]->()
RETURN person, movie
```

Here we find actors who never DIRECTED any movie.

### RETURN Statement

```
MATCH (person:Person)
RETURN person, person.name AS name, toUpper(person.name), coalesce(person.nickname,"n/a") AS nickname,
  { name: person.name, label:head(labels(person))} AS person

+-------------------------------------------------------------------------------------------------------------------------------------------------+
| p                                               | name              | toUpper(p.name)   | nickname | person                                     |
+-------------------------------------------------------------------------------------------------------------------------------------------------+
| (:Person {name: "Keanu Reeves", born: 1964})    | "Keanu Reeves"    | "KEANU REEVES"    | "n/a"    | {name: "Keanu Reeves", label: "Person"}    |
| (:Person {name: "Robert Zemeckis", born: 1951}) | "Robert Zemeckis" | "ROBERT ZEMECKIS" | "n/a"    | {name: "Robert Zemeckis", label: "Person"} |
| (:Person {name: "Tom Hanks", born: 1956})       | "Tom Hanks"       | "TOM HANKS"       | "n/a"    | {name: "Tom Hanks", label: "Person"}       |
+-------------------------------------------------------------------------------------------------------------------------------------------------+
```

If we wish to display only unique results we can use the `DISTINCT`:

```
MATCH (n)
RETURN DISTINCT labels(n) AS Labels

+------------+
| Labels     |
+------------+
| ["Movie"]  |
| ["Person"] |
+------------+
```

### Aggregation

In many cases we wish to aggregate or group the data encountered while traversing patterns in our graph. In Cypher,
**aggregation happens in the RETURN clause** while computing the final results. Many common aggregation functions are
supported, e.g. `count`, `sum`, `avg`, `min`, and `max`, but there are several more.

```
MATCH (:Person)
RETURN count(*) AS people

+--------+
| people |
+--------+
| 3      |
+--------+
1 row
```

> ðŸ“‹ Note that `NULL` values are skipped during aggregation. For aggregating unique values use `DISTINCT`, for example:
> `count(DISTINCT role)`.

#### Collect

A very helpful aggregation function is `collect()`, which collects all the aggregated values into a list.

`collect()` is well-suited for retrieving typical parent-child structures, where one paren is returned per row with all
its dependent information in associated lists created with `collect()`, **which avoids n+1 statements** to retrieve the
parent and its children individually.

The following statement could be used to retrieve the cast of each movie in our database:

```
MATCH (movie:Movie)<-[:ACTED_IN]-(actor:Person)
RETURN movie.title AS movie, collect(actor.name) AS cast, count(*) AS actors

+-----------------------------------------+
| movie          | cast          | actors |
+-----------------------------------------+
| "Forrest Gump" | ["Tom Hanks"] | 1      |
| "Cloud Atlas"  | ["Tom Hanks"] | 1      |
+-----------------------------------------+
```

### Sorting and Pagination

Ordering is done using the `ORDER BY expression [ASC|DESC]` clause. The `expression` can be any expression, as long as
it is computable from the returned information.

Pagination is done using the `SKIP +{offset}+` and `LIMIT +{count}+` clauses.

For instance to find the most prolific actors we could do:

```
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
RETURN actor, count(*) AS appearances
ORDER BY appearances DESC LIMIT 10;
```

### UNION

If you would like to combine the results of two statements that have the same result structure, you can use
`UNION [ALL]`.

For example, the following statement lists both actors and directors:

```
MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)
RETURN actor.name AS name, type(r) AS type, movie.title AS title
UNION
MATCH (director:Person)-[r:DIRECTED]->(movie:Movie)
RETURN director.name AS name, type(r) AS type, movie.title AS title

+-------------------------------------------------+
| name              | type       | title          |
+-------------------------------------------------+
| "Tom Hanks"       | "ACTED_IN" | "Cloud Atlas"  |
| "Tom Hanks"       | "ACTED_IN" | "Forrest Gump" |
| "Robert Zemeckis" | "DIRECTED" | "Forrest Gump" |
+-------------------------------------------------+

3 rows
```

> ðŸ“‹ The query above is equivalent to this more compact query:
>
> ```
> MATCH (actor:Person)-[r:ACTED_IN|DIRECTED]->(movie:Movie)
> RETURN actor.name AS name, type(r) AS type, movie.title AS title
> ```

### Chaining Results

In Cypher it is possible to chain fragments of statements together, similar to how it is done within a data-flow
pipeline. Each fragment works on the output from the previous one, and its results can feed into the next one. Only
columns declared in the `WITH` clause are available in subsequent query parts.

The `WITH` clause is used to combine the individual parts and declare which data flows from one to the other. `WITH` is
similar to the `RETURN` clause. The difference is that the `WITH` clause does not finish the query, but prepares the
input for the next part. Expressions, aggregations, ordering and pagination can be used in the same way as in the
`RETURN` clause. The only difference is all columns must be aliased.

In the example below, we collect the movies someone appeared in, and then filter out those which appear in only one
movie:

```
MATCH (person:Person)-[:ACTED_IN]->(m:Movie)
WITH person, count(*) AS appearances, collect(m.title) AS movies
WHERE appearances > 1
RETURN person.name, appearances, movies

+-------------------------------------------------------------+
| person.name | appearances | movies                          |
+-------------------------------------------------------------+
| "Tom Hanks" | 2           | ["Cloud Atlas", "Forrest Gump"] |
+-------------------------------------------------------------+

1 row
```

## Performance

### Indexing

The main reason for using indexes in a graph database is to find the starting point of a graph traversal. Once that
starting point is found, the traversal relies on in-graph structures to achieve high performance.

Indexes can be added at any time. Note, however, that if there is existing data in the database, it will take some time
for an index to come online.

In this case we want to create an index to speed up finding actors by name in the database:

```
CREATE INDEX FOR (actor:Actor) ON (actor.name)
```

A `composite index` is an index on multiple properties for all nodes that have a particular label. For example, the
following statement will create a composite index on all nodes labeled with Actor and which have both a name and a born
property. Note that since the node with the "Actor" label that has a name of "Keanu Reeves" does not have the born property. Therefore that node will not be added to the index.

```
CREATE INDEX FOR (actor:Actor) ON (actor.name, actor.born)
```

We can inspect our database to find out what indexes are defined. We do this by calling the built-in procedure
`db.indexes`:

```
CALL db.indexes
YIELD description, tokenNames, properties, type;

+---------------------------------------------------------------------------------------+
| description                   | tokenNames | properties       | type                  |
+---------------------------------------------------------------------------------------+
| "INDEX ON :Actor(name)"       | ["Actor"]  | ["name"]         | "node_label_property" |
| "INDEX ON :Actor(name, born)" | ["Actor"]  | ["name", "born"] | "node_label_property" |
+---------------------------------------------------------------------------------------+

2 rows
```

### Constraints

Constraints are used to make sure that the data adheres to the rules of the domain. For example: If a node has a label
of "Movie" and a property of "title", then the value of "title" must be unique among all nodes that have the "Movie"
label:

```
CREATE CONSTRAINT ON (movie:Movie) ASSERT movie.title IS UNIQUE
```

**Adding the unique constraint will implicitly add an index on that property. If the constraint is dropped, but the
index is still needed, the index will have to be created explicitly**.

Constraints can be added to database that already has data in it. This requires that the existing data complies with the
constraint that is being added.

We can inspect our database to find out what constraints are defined. We do this by calling the built-in procedure
`db.constraints`:

```
CALL db.constraints

+--------------------------------------------------------------+
| description                                                  |
+--------------------------------------------------------------+
| "CONSTRAINT ON ( movie:Movie ) ASSERT movie.title IS UNIQUE" |
+--------------------------------------------------------------+

1 row
```